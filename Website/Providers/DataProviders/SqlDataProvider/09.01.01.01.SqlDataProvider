/************************************************************/
/*****              SqlDataProvider                     *****/
/*****                                                  *****/
/*****                                                  *****/
/***** Note: To manually execute this script you must   *****/
/*****       perform a search and replace operation     *****/
/*****       for {databaseOwner} and {objectQualifier}  *****/
/*****                                                  *****/
/************************************************************/


IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'{databaseOwner}[{objectQualifier}AddPasswordHistory]') AND OBJECTPROPERTY(id, N'IsPROCEDURE') = 1)
	DROP PROCEDURE {databaseOwner}{objectQualifier}AddPasswordHistory
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}AddPasswordHistory]
	@UserId             int,
	@Password           nvarchar(128),
	@PasswordSalt       nvarchar(128),
	@PasswordsRetained  int,
	@DaysRetained       int,
	@CreatedByUserID    int
AS
	
	BEGIN
	
	INSERT INTO {databaseOwner}{objectQualifier}PasswordHistory (
		UserId,
		[Password],
		PasswordSalt,
		CreatedByUserID,
		CreatedOnDate,
		LastModifiedByUserID,
		LastModifiedOnDate
	)
	VALUES (
		@UserId,
		@Password,
		@PasswordSalt,
		@CreatedByUserID,
		GETDATE(),
		@CreatedByUserID,
		GETDATE()
	)

	DELETE FROM {databaseOwner}{objectQualifier}PasswordHistory 
	WHERE UserID=@UserId
	  AND PasswordHistoryID NOT IN (
		SELECT TOP (@PasswordsRetained) PasswordHistoryID 
		FROM {databaseOwner}{objectQualifier}PasswordHistory
		WHERE UserID=@UserId 
		ORDER BY CreatedOnDate DESC
		)
	  AND DATEDIFF(day, CreatedOnDate, GETDATE()) > @DaysRetained

	END
GO

IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'{databaseOwner}[{objectQualifier}GetPasswordHistory]') AND OBJECTPROPERTY(id, N'IsPROCEDURE') = 1)
	DROP PROCEDURE {databaseOwner}{objectQualifier}GetPasswordHistory
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}GetPasswordHistory]
	@UserID             int,
	@PasswordsRetained  int,
	@DaysRetained       int
AS
	DELETE FROM {databaseOwner}{objectQualifier}PasswordHistory 
	WHERE UserID=@UserId 
	  AND PasswordHistoryID NOT IN (
		SELECT TOP (@PasswordsRetained) PasswordHistoryID 
		FROM {databaseOwner}{objectQualifier}PasswordHistory
		WHERE UserID=@UserId 
		ORDER BY CreatedOnDate DESC
		)
	  AND DATEDIFF(day, CreatedOnDate, GETDATE()) > @DaysRetained;

	SELECT * 
	FROM {databaseOwner}{objectQualifier}PasswordHistory 
	WHERE UserID=@UserID
GO

/************************************************************/
/*****              SqlDataProvider                     *****/
/************************************************************/